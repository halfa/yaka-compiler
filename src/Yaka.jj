/*
 * Description de la grammaire de YAKA
 */


options{
	JDK_VERSION="1.7";
	OUTPUT_DIRECTORY="compiler";
	LOOKAHEAD=1;
	//DEBUG_PARSER=true;
	//FORCE_LA_CHECK=true;
}

PARSER_BEGIN(Yaka)

package compiler;

public class Yaka {
  	static YVM yvm;
    static TabIdent tabIdent;
    static Expression expression;
    static Declaration declaration;
  
  public static void main(String args[]) {
    Yaka analyseur;
    java.io.InputStream input;
    
    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]);
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Yaka [fichier]");
      return;
    }
    try {
   	  /* Build objet */
      yvm = null; /* will be init later */
      tabIdent = new TabIdent();
      expression = null; /* will be init later */
      declaration = new Declaration();
      
      analyseur = new Yaka(input);
      analyseur.analyse();
      System.out.println("Analyse syntaxique réussie!");
    } catch (ParseException e) {
      String msg = e.getMessage();
      msg = msg.substring(0,msg.indexOf("\n"));
      System.out.println("Erreur de syntaxe : "+msg);
    }
  } 
}

PARSER_END(Yaka)

/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu ,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ YakaTokenManager.entierLu = Integer.parseInt(image.toString()); }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{ YakaTokenManager.identLu =image.toString(); }
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ YakaTokenManager.chaineLue = image.toString(); }
}


/**************************************/
/******** début de la grammaire *******/
/**************************************/

void analyse() : {}
{
   <PROGRAMME> <ident> { Make.begin_program(); }	
   ( declFonction() )*
   <PRINCIPAL> { Make.begin_principal(); }
   bloc()
   <FPRINCIPAL>  { Make.end_program(); }
   <FPROGRAMME>
}

void declFonction() : {}
{
	type() <FONCTION> <ident> { Make.create_fun(); } 
	"(" paramForms() ")"
	bloc()
	<FFONCTION> { Make.closeBlock(); }
}

void paramForms() : {}
{
	( paramForm() 
	  ("," paramForm() )* 
	)?
}

void paramForm() : {}
{
	type() <ident> { Make.new_param(); }
}

void bloc() : {}
{
 (declConst())*
 (declVar())*	{ Make.how_fat_are_you(); }	
   suiteInstr()  // modifié
}

void declConst() : {}
{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}
{
  <ident>  { Make.define_constant(); }
   "=" valConst() 
}

void valConst() : {}{
  <entier> 	{ Make.define_int(); }
 | <ident>	{ Make.define_ident(); }
 | <VRAI> 	{ Make.define_true(); }
 | <FAUX>   { Make.define_false(); }
}


void declVar() : {}
{
  <VAR> type()	
  <ident> { Make.create_var(); }
  (","
      <ident> { Make.create_var(); }
  )* ";"
}

void type() :{}
{
   <ENTIER> { Make.declare_var_int(); }
 | <BOOLEEN> { Make.declare_var_bool(); }
}
/*
 * Syntaxe des instructions.
 */
void suiteExpr() : {}
{ (expression() (";"
  	{Yaka.expression.clear();}
  (expression())? )*)? }

void suiteInstr() : {}
{ (instruction() (";"
	  {Yaka.expression.clear();}
  (instruction())? )*)? }

void instruction() : {}
{ affectation() | lecture() | ecriture() | iteration() | conditionnelle() | retourne() }

void retourne() : {}
{
	<RETOURNE> expression() { Make.putReturn(); }
}

void conditionnelle() : {}
{
  <SI> {Make.startIf();} expression() {Make.assertType(Type.BOOLEAN);}
  <ALORS> {Make.condIf();} suiteInstr()
  ( 
    (<SINON> {Make.elseIf();} 
      suiteInstr() 
    <FSI> {Make.endIf();}) 
    | (<FSI> {Make.elseIf(); Make.endIf();} )
  )
}

void iteration() : {}
{ <TANTQUE> 			{Make.startLoop();} 
expression()  			{Make.condLoop();Make.assertType(Type.BOOLEAN);}
<FAIRE> suiteInstr() 
<FAIT> 					{Make.endLoop();} }

void affectation() : {}
{ <ident> {Declaration.setCurrentIdent(YakaTokenManager.identLu);} "=" expression() {Make.assertAssignementType();Make.assignment();} }

void lecture() : {}
{ <LIRE> "(" <ident> ")" {Make.reading();}
}

void ecriture() :{}
{ <ECRIRE>( "(" ( expression() {Make.write_expression(Yaka.expression);} 
| <chaine> { Make.write_string(); } ) ")" ) 
| <ALALIGNE> {Make.aLaLigne();} }

/*
 * Expression
 */
void expression() : {}
{
  simpleExpr() 
  (opRel() 
  simpleExpr() {Make.evaluate();}
  )?
}

void simpleExpr() : {}
{
   terme() 
   (opAdd() 
   terme() {Make.evaluate();}
   )*
} 
void terme() : {}
{
  facteur() 
  (opMul() 
  facteur()	{Make.evaluate();}
  )*
}
void facteur() : {}
{   primaire() 
  |  opNeg() primaire() {Make.evaluate();}
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")"
 
}

void valeur () : {}
{ <entier> 	{ Make.value_int();Yaka.expression.pushValue(Type.INTEGER);}
 | <ident> 	( {Make.begin_function_call();Make.pushFunctionType();} "(" argumentsFonction() { Make.callFunction(); } ")" 
            | /* juste une variable */ { Make.value_ident();Make.pushIdentType();} )
 | <VRAI> 	{ Make.value_true();Yaka.expression.pushValue(Type.BOOLEAN);}
 | <FAUX> 	{ Make.value_false();Yaka.expression.pushValue(Type.BOOLEAN);}
}

void argumentsFonction() :{}
{
	(expression() ("," expression())*)? {Make.checkArgumentType();}

}


void opRel() : {}
{
   "=" 		{Yaka.expression.pushOp(Operator.EGAL);}
 | "<>" 	{Yaka.expression.pushOp(Operator.DIFF);}
 | "<" 		{Yaka.expression.pushOp(Operator.LOW_STRICT);}
 | "<=" 	{Yaka.expression.pushOp(Operator.LOW_EGAL);}
 | ">" 		{Yaka.expression.pushOp(Operator.UP_STRICT);}
 | ">=" 	{Yaka.expression.pushOp(Operator.LOW_EGAL);}
} 

void opAdd() : {}
{
    "+"		{Yaka.expression.pushOp(Operator.PLUS);}
  |  "-"  	{Yaka.expression.pushOp(Operator.MINUS);}
  | <OU> 	{Yaka.expression.pushOp(Operator.OR);}
}

void opMul() : {}
{
  "*"		{Yaka.expression.pushOp(Operator.MULT);}
  | "/"		{Yaka.expression.pushOp(Operator.DIV);}
  | <ET>	{Yaka.expression.pushOp(Operator.AND);}
} 

void opNeg() : {}
{ "-" 		{Yaka.expression.pushOp(Operator.OPP);}
 | <NON>	{Yaka.expression.pushOp(Operator.NOT);}
}
